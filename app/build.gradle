apply plugin: 'com.android.application'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    Properties versionProps = new Properties()
    def versionPropsFile = file("${project.rootDir}/version.properties")
    versionProps.load(new FileInputStream(versionPropsFile))
    def value = 0
    def runTasks = gradle.startParameter.taskNames
    if ('assemble' in runTasks || 'assembleRelease' in runTasks) {
        value = 1
    }
    def versionMajor = 1
    def versionPatch = versionProps['VERSION_PATCH'].toInteger() + 1
    def versionBuild = versionProps['VERSION_BUILD'].toInteger()
    def versionNumber = versionProps['VERSION_NUMBER'].toInteger() + value

    if (versionPatch % 100 == 0) {
        versionBuild += 1
        versionPatch = 1
    }

    versionProps['VERSION_PATCH'] = versionPatch.toString()
    versionProps['VERSION_BUILD'] = versionBuild.toString()
    versionProps['VERSION_NUMBER'] = versionNumber.toString()
    versionProps.store(versionPropsFile.newWriter(), null)

    defaultConfig {
        applicationId "com.frestoinc.sampleapp"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode versionNumber
        versionName "${versionMajor}.${versionPatch}.${versionBuild}(${versionNumber})"
        archivesBaseName = versionName
        setProperty("archivesBaseName", "${applicationId}-v${versionName}")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            resValue "string", "app_version", "${defaultConfig.versionName}${".debug"}"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            debuggable = false
            shrinkResources true
            resValue "string", "app_version", "${defaultConfig.versionName}"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        animationsDisabled true
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    lintOptions {
        lintConfig file("${project.rootDir}/config/lint/lint.xml")
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

apply plugin: 'checkstyle'

checkstyle {
    toolVersion = '8.24'
}

task checkstyle(type: Checkstyle) {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'
    classpath = files()
    ignoreFailures = false
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.1.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'androidx.work:work-runtime:2.4.0'
    implementation 'androidx.concurrent:concurrent-futures:1.1.0'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    /**
     * TEST LIBRARIES
     */
    testImplementation 'junit:junit:4.13'

    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.ext:truth:1.3.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'

    androidTestImplementation 'com.google.truth:truth:1.0'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'

    testImplementation 'org.mockito:mockito-core:2.27.0'
    androidTestImplementation 'org.mockito:mockito-android:2.23.4'
    testImplementation 'net.bytebuddy:byte-buddy-dep:1.9.7'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"

    androidTestImplementation 'androidx.work:work-testing:2.4.0'


    /**
     * Gson
     */
    def gson_version = "2.8.6"
    implementation "com.google.code.gson:gson:$gson_version"

    /**
     *  ROOM
     */
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    /**
     *  RxAndroid
     */
    def rx_android_version = "2.1.1"
    implementation "io.reactivex.rxjava2:rxandroid:$rx_android_version"

    /**
     *  RxJava
     */
    def rx_java_version = "2.2.17"
    implementation "io.reactivex.rxjava2:rxjava:$rx_java_version"

    /**
     *  Materials
     */
    def materials_version = "1.2.1"
    implementation "com.google.android.material:material:$materials_version"

    /**
     *  LiveData and ViewModel
     */
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$lifecycle_version"

    /**
     * HILT
     */
    implementation "com.google.dagger:hilt-android:$hilt_version"
    annotationProcessor "com.google.dagger:hilt-android-compiler:$hilt_version"

    /**
     * Retrofit
     */
    def retrofit_version = "2.7.1"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    /**
     *  OkHttp3
     */
    def okhttp_version = "3.12.0"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"

    /**
     * Glide
     */
    def glide_version = "4.11.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    def rxwork_version = "2.3.0"
    implementation "androidx.work:work-rxjava2:$rxwork_version"
}
